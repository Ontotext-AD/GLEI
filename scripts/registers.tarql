prefix glei:              <http://data.ontotext.com/resource/glei/>
prefix fibo-fnd-org-org:  <http://www.omg.org/spec/EDMC-FIBO/FND/Organizations/Organizations/>
prefix fibo-fnd-rel-rel:  <http://www.omg.org/spec/EDMC-FIBO/FND/Relations/Relations/>
prefix fibo-fbc-fct-breg: <http://www.omg.org/spec/EDMC-FIBO/FBC/FunctionalEntities/BusinessRegistries/>
prefix fibo-fnd-law-jur:  <http://www.omg.org/spec/EDMC-FIBO/FND/Law/Jurisdiction/>
prefix fibo-fnd-aap-agt:  <http://www.omg.org/spec/EDMC-FIBO/FND/AgentsAndPeople/Agents/>
prefix fibo-be-corp-corp: <http://www.omg.org/spec/EDMC-FIBO/BE/Corporations/Corporations/>
prefix rdfs:              <http://www.w3.org/2000/01/rdf-schema#>
prefix lcc-3166-1:        <http://www.omg.org/spec/LCC/Countries/ISO3166-1-CountryCodes/>
prefix lcc-3166-2:        <http://www.omg.org/spec/LCC/Countries/ISO3166-2-SubdivisionCodes/>
prefix leio:              <http://data.ontotext.com/resource/leio/>
prefix xsd:               <http://www.w3.org/2001/XMLSchema#>

construct {
 ?manage a fibo-fnd-org-org:Organization;
         fibo-fnd-rel-rel:hasLegalName ?manage_label_en;
         fibo-fnd-rel-rel:hasLegalName ?Local_name_of_organisation_responsible_for_the_Register;
         fibo-fnd-rel-rel:manages ?register.
 ?register a fibo-fbc-fct-breg:BusinessRegistrationAuthority;
           fibo-fnd-law-jur:appliesIn ?jurisdiction;
           fibo-fnd-rel-rel:hasLegalName ?label_en;
           fibo-fnd-rel-rel:hasLegalName ?Local_name_of_Register;
           leio:dateIPdisclaimerIncluded ?date;
           rdfs:comment ?Comments;
           rdfs:seeAlso ?web;
           fibo-fnd-aap-agt:isIdentifiedBy ?id;
           fibo-fbc-fct-breg:hasCountry ?cntry.
 ?id a fibo-be-corp-corp:RegistrationIdentifier;
     fibo-fnd-rel-rel:hasUniqueIdentifier ?Registration_Authority_Code.
}
where {
 bind(1+"undef" as ?undef)
 BIND(uri(concat("http://data.ontotext.com/resource/glei/organization/",
    IF (BOUND(?International_name_of_organisation_responsible_for_the_Register),
        ENCODE_FOR_URI(?International_name_of_organisation_responsible_for_the_Register),
        ENCODE_FOR_URI(?Local_name_of_organisation_responsible_for_the_Register)))) as ?manage)
 BIND(STRLANG(?International_name_of_organisation_responsible_for_the_Register, "en") as ?manage_label_en)
 BIND(URI(CONCAT("http://data.ontotext.com/resource/glei/register/", ?Registration_Authority_Code)) as ?register)
 BIND(URI(CONCAT("http://www.omg.org/spec/LCC/Countries/ISO3166-2-SubdivisionCodes/",
                 IF(replace(?Country, "^[ \t]+|[ \t]+$", "") != replace(?Jurisdiction_country_or_region, "^[ \t]+|[ \t]+$", ""),
                 # replace(?Jurisdiction_country_or_region, "([ ]+)([^ ])", "\U$2"),
                 ?JurisdictionCountryOrRegion,
                  ?undef))) as ?jurisdiction)
 BIND(STRLANG(?International_name_of_Register, "en") as ?label_en)
 BIND(URI(REPLACE(?Website,"^ *| *$","")) as ?web)
 BIND(URI(CONCAT(str(?register), "/id")) as ?id)
 BIND(URI(CONCAT("http://www.omg.org/spec/LCC/Countries/ISO3166-1-CountryCodes/", ?Country_Code)) as ?cntry)
 # format - December 23, 2013
 BIND(replace(?Date_IP_disclaimer_included, "^([^ ]+)[^0-9]+([0-9]+)[^0-9]+([0-9]+)$", "$3") as ?year)
 BIND(replace(?Date_IP_disclaimer_included, "^([^ ]+)[^0-9]+([0-9]+)[^0-9]+([0-9]+)$", "$2") as ?day_unnormalized)
 BIND(replace(?Date_IP_disclaimer_included, "^([^ ]+)[^0-9]+([0-9]+)[^0-9]+([0-9]+)$", "$1") as ?month_label)
 BIND(IF (?month_label = "January",  "01",
      IF (?month_label = "February", "02",
      IF (?month_label = "March",    "03",
      IF (?month_label = "April",    "04",
      IF (?month_label = "May",      "05",
      IF (?month_label = "June",     "06",
      IF (?month_label = "July",     "07",
      IF (?month_label = "August",   "08",
      IF (?month_label = "September","09",
      IF (?month_label = "October",  "10",
      IF (?month_label = "November", "11",
      IF (?month_label = "December", "12","")))))))))))) as ?month)
 BIND(IF(regex(?day_unnormalized, "^(.)$"), replace(?day_unnormalized, "^(.)$", "0$1"), ?day_unnormalized) as ?day)
 BIND(strdt(concat(?year,"-", ?month, "-", ?day), xsd:date) as ?date)

}

# | Registration_Authority_Code | Country | Country_Code | Jurisdiction_(country_or_region) | International_name_of_Register | Local_name_of_Register | International_name_of_organisation_responsible_for_the_Register | Local_name_of_organisation_responsible_for_the_Register | Website | Date_IP_disclaimer_included_http://www.leiroc.org/publications/gls/lou_20131211.pdf | Comments |
